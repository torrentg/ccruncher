dnl *************************************************************
dnl
dnl Process this file with autoconf to produce a configure script
dnl 
dnl *************************************************************

dnl -------------------------------------------------------------
dnl initializing procedures
dnl -------------------------------------------------------------
AC_PREREQ(2.59)
AC_INIT(ccruncher, 0.1, gerard@mail.generacio.com)
AC_CONFIG_SRCDIR([src/kernel/MonteCarlo.hpp])
AC_CONFIG_HEADER([src/utils/config.h],,[src/utils/config.h.in])
AM_INIT_AUTOMAKE(1.6.1)

dnl -------------------------------------------------------------
dnl rebuild makefile prevention 
dnl solve automake versions conflicts
dnl -------------------------------------------------------------
AM_MAINTAINER_MODE

dnl -------------------------------------------------------------
dnl check for tools
dnl -------------------------------------------------------------
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_INSTALL

dnl -------------------------------------------------------------
dnl checks for libraries
dnl -------------------------------------------------------------
AC_CHECK_LIB([m], [main])
AC_CHECK_LIB([dl], [main])
AC_CHECK_LIB([xerces-c], [main],,
    [AC_MSG_ERROR([cannot find the xerces-c library]) ])
AC_CHECK_HEADERS(xercesc/util/XercesVersion.hpp,,
    [AC_MSG_ERROR([cannot find xerces-c headers]) ])

dnl -------------------------------------------------------------
dnl checks for header
dnl -------------------------------------------------------------
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([limits.h stdlib.h unistd.h])

dnl -------------------------------------------------------------
dnl checks for functions
dnl -------------------------------------------------------------
AC_CHECK_FUNCS([getcwd])
AC_CHECK_FUNCS([mkdir])
AC_CHECK_FUNCS([pow])
AC_CHECK_FUNCS([sqrt])
AC_CHECK_FUNCS([strtol])
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_ERROR_AT_LINE
AC_FUNC_STRTOD

dnl -------------------------------------------------------------
dnl other checks (consts, structs, types)
dnl -------------------------------------------------------------
AC_HEADER_STDBOOL
AC_C_CONST
AC_C_INLINE
AC_C_RESTRICT
AC_STRUCT_TM
AC_TYPE_SIZE_T

dnl -------------------------------------------------------------
dnl setting debug/optimization options
dnl   optimized mode => -O2 (default)
dnl   debug mode => -g -pg
dnl -------------------------------------------------------------
AC_ARG_ENABLE(debug, 
  [  --enable-debug    Turn on debugging [default=no]],
  [case "${enableval}" in
    yes) debug=true ;;
    no)  debug=false ;;
    *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
  esac],
  [debug=false])

if test x$debug = xtrue ; then
  CXXFLAGS="-g -pg"
else
  CXXFLAGS="-O2"
  AC_DEFINE([NDEBUG], 1, [Assertions are disabled when this is defined.])
fi
AC_SUBST(CXXFLAGS)


dnl -------------------------------------------------------------
dnl defining include directories
dnl -------------------------------------------------------------
AC_SUBST([AM_CPPFLAGS], 
         ['-I$(top_srcdir)/share/jama-1.2.2 -I$(top_srcdir)/share/Mersenne-1.0 -I$(top_srcdir)/share/MiniCppUnit-2.2]')
    
dnl -------------------------------------------------------------
dnl makefile's to be built
dnl -------------------------------------------------------------
AC_CONFIG_FILES([Makefile src/Makefile])
AC_OUTPUT
